const STATE_INITIAL = 0
const STATE_STARTED_QUEST = 1
const STATE_FOUND_BANDIT = 5
const STATE_DEFEATED_BANDIT = 2
const STATE_WAKE_UP = 6
const STATE_WENT_DOWNSTAIRS = 7
const STATE_ARTHUR_FIGHT = 3
const STATE_FINAL = 4

const POS_HIDDEN_X = 0
const POS_HIDDEN_Y = 0

const LOCALID_ARTHUR_SISTER = 1
const LOCALID_ARTHUR_DAD = 5
const LOCALID_ARTHUR_MOM = 6
const LOCALID_ARTHUR = 4
const LOCALID_PUDDLETRAINER = 2
const LOCALID_PUDDLEMON = 3
const LOCALID_AZURILL = 13

mapscripts ISLANDGAME_ROSEVALE_MapScripts {
    // todo: consider using on_warp_into_map_table instead of map_script_on_transition
    MAP_SCRIPT_ON_TRANSITION {
        switch (var(VAR_ROSEVALE_STATE)) {
            case STATE_STARTED_QUEST:
                // only sister and dad blocking inn are visible
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 8, 14)
                addobject(LOCALID_ARTHUR_SISTER)
                setobjectmovementtype(LOCALID_ARTHUR_SISTER, MOVEMENT_TYPE_FACE_LEFT)
            case STATE_DEFEATED_BANDIT:
                // everyone is standing around the inn
                setobjectxyperm(LOCALID_ARTHUR, 12, 26)
                setobjectxyperm(LOCALID_ARTHUR_DAD, 13, 24)
                setobjectxyperm(LOCALID_ARTHUR_MOM, 12, 24)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 14, 24)
                setobjectxyperm(LOCALID_AZURILL, 14, 26)
                setobjectmovementtype(LOCALID_ARTHUR, MOVEMENT_TYPE_FACE_UP)
                setobjectmovementtype(LOCALID_AZURILL, MOVEMENT_TYPE_FACE_UP)
                setobjectmovementtype(LOCALID_ARTHUR_DAD, MOVEMENT_TYPE_FACE_DOWN)
                addobject(LOCALID_ARTHUR)
                addobject(LOCALID_ARTHUR_DAD)
                addobject(LOCALID_ARTHUR_MOM)
                addobject(LOCALID_ARTHUR_SISTER)
                addobject(LOCALID_AZURILL)
                turnobject(LOCALID_ARTHUR, DIR_NORTH)
                turnobject(LOCALID_AZURILL, DIR_NORTH)
            case STATE_FINAL:
                // everyone is hidden, inn is open
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, POS_HIDDEN_X, POS_HIDDEN_Y)
            case STATE_FOUND_BANDIT:
                // only sister and dad blocking inn are visible
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 8, 14)
                addobject(LOCALID_ARTHUR_SISTER)
                setobjectmovementtype(LOCALID_ARTHUR_SISTER, MOVEMENT_TYPE_FACE_LEFT)
            default:

        }

        if (var(VAR_ROSEVALE_STATE) != STATE_ARTHUR_FIGHT && var(VAR_ROSEVALE_STATE) != STATE_FINAL && var(VAR_ROSEVALE_STATE) != STATE_DEFEATED_BANDIT) {
                setobjectxyperm(LOCALID_ARTHUR_DAD, 13, 24)
                setobjectmovementtype(LOCALID_ARTHUR_DAD, MOVEMENT_TYPE_FACE_LEFT_AND_RIGHT)
                addobject(LOCALID_ARTHUR_DAD)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ROSEVALE_STATE, STATE_DEFEATED_BANDIT {
            setvar(VAR_ROSEVALE_STATE, STATE_WAKE_UP)

            turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
            waitmovement(0)
            applymovement(LOCALID_ARTHUR_SISTER, Common_Movement_FaceDown)
            waitmovement(0)
            playse(SE_PIN)
            applymovement(LOCALID_ARTHUR_SISTER, Common_Movement_ExclamationMark)
            waitmovement(0)
            applymovement(LOCALID_AZURILL, Common_Movement_FaceUp)
            waitmovement(0)
            applymovement(LOCALID_AZURILL, Common_Movement_ExclamationMark)
            waitmovement(0)
            playmoncry(SPECIES_AZURILL, CRY_MODE_NORMAL)
            waitmoncry
            applymovement(LOCALID_AZURILL, Movement_AzurillRunsToSister)
            waitmovement(0)
            applymovement(LOCALID_ARTHUR_SISTER, Movement_SisterHappyToSee)
            waitmovement(0)
            applymovement(LOCALID_ARTHUR_SISTER, Common_Movement_FaceDown)
            msgbox(format("{COLOR GREEN}Arthur's Sister: {COLOR DARK_GRAY}Hooway!! You did it! You saved A-zoo-will and Arfur!"))


            // everyone thanks you for helping
            applymovement(LOCALID_ARTHUR_MOM, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            applymovement(LOCALID_ARTHUR_MOM, Common_Movement_Delay48)
            waitmovement(0)
            msgbox(format("{COLOR GREEN}Arthur's Mom: {COLOR DARK_GRAY}Thank you so much for helping our son and rescuing our daughter's Pokémon, dear. How can we ever repay you?"))
            closemessage

            applymovement(LOCALID_ARTHUR_DAD, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            applymovement(LOCALID_ARTHUR_DAD, Common_Movement_Delay48)
            waitmovement(0)
            msgbox(format("{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}Please, come stay the night at our inn, free of charge! It's the least we can do for your kindness."))
            closemessage

            applymovement(LOCALID_ARTHUR, Common_Movement_FaceRight)
            applymovement(LOCALID_ARTHUR, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            applymovement(LOCALID_ARTHUR, Common_Movement_Delay48)
            waitmovement(0)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
            msgbox(format(
                "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Watching your Pokémon battle...{PAUSE 30} it was incredible.\p"
                "They knew exactly what to do in the fight, even in the heat of the moment.\p"
                "You must be a pretty seasoned Pokémon trainer, right? What brings you here to Rosevale of all places?"
            ))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
            waitmovement(0)

            msgbox(format(
                "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Oh...{PAUSE 30} you were training up your Pokemon to battle against Tidalcove's Gym Leader, Ruka?\p"
                "I see...{PAUSE 30} that means you're taking part in the Festival Tournament then, right?\p"
                "Hm...{PAUSE 30} So Pokémon trainers can get stronger by battling Gym Leaders...\p"
                "Oh, sorry! We should probably head inside. Let's continue this conversation after dinner!"
            ))
            closemessage

            fadescreen(FADE_TO_BLACK)
            msgbox(format("{COLOR RED}(I spent the rest of the night talking to Arthur about my journey thus far...)"))
            closemessage
            //warp
            playfanfare(MUS_HEAL)
            waitfanfare
            special(HealPlayerParty)
            completequest(QUEST_ROSEVALE_RESCUE)
            setrespawn(HEAL_LOCATION_ISLANDGAME_ROSEVALE) // so you can't skip arthur if you lose
            warp(MAP_ISLANDGAME_ROSEVALE_INN_2F_ROOM1, 2, 2)
        }
    ]
}

movement Movement_AzurillRunsToSister {
    jump_in_place_up
    walk_fast_right
    walk_fast_up * 2
    face_left
}

movement Movement_SisterHappyToSee {
    face_right
    jump_in_place_up
    jump_in_place_up
    delay_16 * 3
}

script ISLANDGAME_ROSEVALE_Interact_PuddleMon {
    lock
    faceplayer
    playmoncry(SPECIES_CHARMANDER, CRY_MODE_WEAK)
    msgbox(format("Char... {COLOR BLUE}(water scary :c)"))
    release
}

movement Movement_TrainerJump {
    jump_in_place_left
    jump_in_place_up
    jump_in_place_right
    jump_in_place_down
    jump_in_place_left
}

movement Movement_MonWatch {
    face_right
}

movement Movement_MonJump {
    jump_in_place_right
}

script ISLANDGAME_ROSEVALE_Interact_PuddleTrainer {
    lock
    msgbox(format("It's ok! The water isn't going to hurt you...\nWatch!"))
    closemessage
    applymovement(LOCALID_PUDDLEMON, Movement_MonWatch)
    applymovement(LOCALID_PUDDLETRAINER, Movement_TrainerJump)
    waitmovement(0)
    msgbox(format("See?\nIt's shallow!"))
    closemessage
    applymovement(LOCALID_PUDDLEMON, Movement_MonJump)
    playmoncry(SPECIES_CHARMANDER, CRY_MODE_WEAK)
    waitmoncry
    waitmovement(0)
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC1 {
    lock
    faceplayer
    msgbox(format(
        "Ew, a city-dweller. Keep your city germs away from our clean town!"
    ))
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC2 {
    lock
    msgbox(format(
        "Brrr...{PAUSE 30} The winds are always so chilly here.\p"
        "Talonflame! Warm up my hands, will ya? I hate being on watch duty..."
        )
    )
    release
}

script ISLANDGAME_ROSEVALE_Interact_Talonflame {
    lock
    faceplayer
    playmoncry(SPECIES_TALONFLAME, CRY_MODE_NORMAL)
    msgbox(format("Talooon!"))
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC3 {
    lock
    faceplayer
    msgbox(format(
        "La dee dum... I love the sweet fragrance of flowers!\p"
        "Would you like to take a whiff?\p"
        "...Oops, sorry. I didn't know you were allergic."
        )
    )
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC4 {
    lock
    faceplayer
    msgbox(format(
        "My gramps said I needed to “touch grass” more, so here I am.\p"
        "I just wanna go back inside and tend to my farm on Starfdew Valley."
    ))
    release
}

script ISLANDGAME_ROSEVALE_Interact_EntranceSign {
    msgbox(format("ROSEVALE\nThe village under the alps"), MSGBOX_SIGN)
}

script ISLANDGAME_ROSEVALE_Interact_InnSign {
    msgbox(format("PEACHY SWEET INN\nWe're positively peachy keen!\n"), MSGBOX_SIGN)
}

script ISLANDGAME_ROSEVALE_Interact_ArthurSister {
    msgbox(format("My bwother went that-a-way! I think he wan into Mount Awowa!"), MSGBOX_NPC)
}

script ISLANDGAME_ROSEVALE_Interact_ArthurDad {
    msgbox(format("Where in the world could that boy be...?"), MSGBOX_NPC)
}



// section: sister cutscene pre-aurora
text Text_ArthurSister_PleaseHelp {
    format(
        "You're a twainer, right? Someone stole my A-zoo-wil! \p"
        "My bwother Arfur wan after the thief, but he still hasn't come back!\p"
        "He told me not to wowwy, but I'm getting weally wowwied now!\p"
        "Pwease help me find him! And my A-zoo-wil!"
    )
}

movement Movement_ArthurSister_RunUpToPath {
    walk_fast_left * 6
}

script ISLANDGAME_ROSEVALE_Trigger_TryLeaveWithoutQuest {
    lock
    msgbox(format("Hewwo, I need your help!"))
    closemessage
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    // the sister runs up
    addobject(LOCALID_ARTHUR_SISTER)
    setobjectxy(LOCALID_ARTHUR_SISTER, 14, 14)
    applymovement(LOCALID_ARTHUR_SISTER, Movement_ArthurSister_RunUpToPath)
    waitmovement(LOCALID_ARTHUR_SISTER)

    // she asks if you can help
    msgbox(Text_ArthurSister_PleaseHelp)

    // quest starts
    startquest(QUEST_ROSEVALE_RESCUE)
    setvar(VAR_ROSEVALE_STATE, STATE_STARTED_QUEST)

    release
}

// section: arthur fight
movement Movement_Arthur_WalkUpToExit {
    walk_right * 7
}

script ISLANDGAME_ROSEVALE_Trigger_TryLeaveArthurFight {
    lock
    // arthur walks up
    msgbox(format("{COLOR GREEN}Arthur: {COLOR DARK_GRAY}{PLAYER}!"))
    closemessage
    addobject(LOCALID_ARTHUR)
    setobjectxy(LOCALID_ARTHUR, 19, 12)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_WalkUpToExit)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    waitmovement(0)

    // arthur explains situation
    msgbox(format(
        "Before you go...\p"
        "Please...\p"
        "Face me in a Pokémon battle!"
        "I couldn't do anything against that thief...{PAUSE 30} I failed my little sister...\p"
        "If it wasn't for you and Articuno...{PAUSE 30} My sister's Pokémon would've been lost forever!\p"
        "I can't something like that happen again...{PAUSE 30} I need to get stronger, so when the time comes...{PAUSE 30} I'll be able to protect her myself!\p"
        "And there's no better way than for me to take on the Marya League and enter the Festival Tournament, just like you!\p"
        "My dad says I'm not ready yet..."
        "So I'll prove it to him! Right here and now! Prepare yourself, {PLAYER}!"
    ), MSGBOX_NPC)

    trainerbattle_no_intro(TRAINER_ROSEVALE_ARTHUR, format("...\pI lost again..."))


    // arthur talks about fight
    msgbox(format(
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}...\p"
        "{COLOR DARK_GRAY}Heh...{PAUSE 30} You're truly an amazing trainer, {PLAYER}.\p"
        "I have no doubt that you'll be able to beat Ruka.\p"
        "{COLOR DARK_GRAY}Me on the other hand? I should just give up.\p"
        "{COLOR DARK_GRAY}Maybe I'm just not cut out to be a Pokémon trainer.\p"
        "I never win...{PAUSE 30} Why can't I ever win? That thief was right...{PAUSE 30} I am just a loser.\p"
        "...a failure.\p"
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}No, Arthur, that's not true."
    ), MSGBOX_NPC)
    closemessage

    // dad walks up
    addobject(LOCALID_ARTHUR_DAD)
    setobjectxy(LOCALID_ARTHUR_DAD, 19, 12)
    applymovement(LOCALID_ARTHUR_DAD, Movement_ArthurDad_WalkUpToArthur)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_SeesDad)
    waitmovement(LOCALID_ARTHUR_DAD)
    waitmovement(LOCALID_ARTHUR)

    // messages between dad, arthur, player
    msgbox(format(
        "Losing just means you have more to learn. It doesn't mean you're a failure.\p"
        "Failure only comes when you give up. When you stop believing.\p"
        "I watched your battle with {PLAYER}. I can tell you still don't trust in your Pokémon, or even yourself.\p"
        "And if you don't trust in yourself, how can you expect your Pokémon to?\p"
        "That's why you need more time. You have to build that trust with your Pokémon. Build that trust with yourself. Do you understand?\p"
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}-sniff- Mhm."
    ), MSGBOX_NPC)

    msgbox(format(
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}I know I may have been a little harsh on you earlier, but it's only because I want the best for you.\p"
        "Pokémon are a big responsibility, and I want to make sure that you can take care of both yourself and your Pokémon before you enter the Gym Challenge.\p"
        "In the next week, if you show me that the bond between you and your Pokemon has strengthened, I will let you challenge Ruka.\p" 
        "And if you win, I will let you resume the rest of the gym challenge on your own. Does that sound fair?\p"
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Okay. I'll train really really hard.\p"
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}It has been a pleasure, {PLAYER}.\pI wish you all the best in your travels."
    ), MSGBOX_NPC)

    applymovement(LOCALID_ARTHUR, Common_Movement_FaceRight)
    waitmovement(0)
    msgbox(format(
        "{COLOR GREEN}Arthur:{COLOR DARK_GRAY} {PLAYER}! I swear the next time we battle, I'll be stronger!"
    ))
    closemessage
    
    // dad and arthur walk away
    applymovement(LOCALID_ARTHUR_DAD, Movement_ArthurDad_WalkAway)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_WalkAway)
    waitmovement(LOCALID_ARTHUR)
    waitmovement(LOCALID_ARTHUR_DAD)
    removeobject(LOCALID_ARTHUR)
    removeobject(LOCALID_ARTHUR_DAD)

    setvar(VAR_ROSEVALE_STATE, STATE_FINAL)
    release
}

movement Movement_ArthurDad_WalkUpToArthur {
    walk_right * 6
}

movement Movement_Arthur_SeesDad {
    delay_16 * 4
    face_left
    emote_exclamation_mark
}

movement Movement_ArthurDad_WalkAway {
    walk_left * 7
}

movement Movement_Arthur_WalkAway {
    delay_16 * 4
    walk_left * 8
}

script(local) ArthurDefeated {

}