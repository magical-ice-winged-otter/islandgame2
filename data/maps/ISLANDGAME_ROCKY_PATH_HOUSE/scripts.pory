const QUEST_ROCKY_PATH_NURSE_NPC = 1

mapscripts ISLANDGAME_ROCKY_PATH_HOUSE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        quest_emote_on_warp(QUEST_ROCKY_PATH_NURSE_NPC, QUEST_ROCKY_PATH_NURSE)
    }
}

script ISLANDGAME_ROCKY_PATH_HOUSE_Nurse {
    lock
    returnqueststate(QUEST_ROCKY_PATH_NURSE)

    switch(var(VAR_RESULT)) {
        case QUEST_INACTIVE:
            msgbox(format(
                "Mmngh... where am I going to find a Balm Mushroom of all things...?\p"
                "It's the last ingredient I need to complete my medicine!\p"
                "Hmmn?"
            ))
            closemessage
            faceplayer
            applymovement(1, Common_Movement_QuestionMark)
            waitmovement(0)
            applymovement(1, Common_Movement_Delay48)
            waitmovement(0)
            msgbox(format(
                "My apologies, trainer. This is not a center, so I cannot heal your Pokémon.\p"
                "...\p"
                "...Oh?{PAUSE 30} You're interested in what I'm working on?\p"
                "I am currently in the works of developing a concoction that can be distributed to trainers to heal their Pokémon without going to a center.\p"
                "After weeks of experimentation, I was able to craft a formula, but there is one rather cumbersome issue.\p"
                "In order for my concoction to work, I need a Balm Mushroom, which are extremely difficult to find in the wild.\p"
                "-sigh- I would sooner find a Pokémon with Pokérus before one of those..."
            ))
            startquest(QUEST_ROCKY_PATH_NURSE)
        case QUEST_ACTIVE:
            checkitem(ITEM_BALM_MUSHROOM)
            if(var(VAR_RESULT)) {
                msgbox(format(
                    "{COLOR RED}(Should I give my Balm Mushroom to Nurse Joy...?)"
                ), MSGBOX_YESNO)
                if(var(VAR_RESULT) == YES) {
                    faceplayer
                    applymovement(1, Common_Movement_ExclamationMark)
                    waitmovement(0)
                    msgbox(format(
                        "O-Oh!{PAUSE 30} You managed to find one!? I am forever in your debt, trainer!"
                    ))
                    closemessage
                    applymovement(1, Common_Movement_FaceRight)
                    waitmovement(0)
                    applymovement(1, Common_Movement_Delay48)
                    waitmovement(0)
                    msgbox(format(
                        "{PAUSE 60}Alright... {PAUSE 60}Just a little more...{PAUSE 60} annnd...{PAUSE 60} voila!{PAUSE 30} It is finished!"
                    ))
                    faceplayer
                    msgbox(format(
                        "Trainer, I present to you, the newest breakthrough in Pokémon Medicine...\p"
                        "The PokéVial!"
                    ))
                    giveitem(ITEM_POKEVIAL)
                    msgbox(format(
                        "Inside of this vial is enough of my special concoction that will heal all Pokémon in your party to full health, just a trip to the Pokémon Center!\p"
                        "Oh, and don't worry about running out of it, either! I plan on stocking up every center in Marya with my medicine, so you'll be able to get a refill whenever you get a normal heal!"
                    ))
                    completequest(QUEST_ROCKY_PATH_NURSE)
                }
            }
            else {
                faceplayer
                msgbox(format(
                    "Until I find a Balm Mushroom, my concoction will never be complete..."
                ))
            }
        case QUEST_COMPLETED:
            faceplayer
            msgbox(format(
                "Thank you for your assistance, trainer. Your contribution will save the lives of countless future Pokémon in need."
            ))
        default:
    }
    closemessage
    release
}


script ISLANDGAME_ROCKY_PATH_HOUSE_Happiny {
    lock
    faceplayer
    playmoncry(SPECIES_HAPPINY, CRY_MODE_NORMAL)
    if(!flag(FLAG_ROCKY_PATH_HAPPINY_GIFT)) {
        msgbox(format(
            "Hapi! {COLOR BLUE}(I might not have healing capabilities, but this does!)"
        ))
        giveitem(ITEM_FULL_RESTORE)
        setflag(FLAG_ROCKY_PATH_HAPPINY_GIFT)
    }
    else {
        msgbox(format(
            "Hapi Hapi. {COLOR BLUE}(I hope it will be useful in your journey. :D)"
        ))
    }
    closemessage
    release
}