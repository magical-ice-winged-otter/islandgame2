mapscripts ISLANDGAME_Southern_Oranna_Path_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_TEMP_1, 0)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: ISLANDGAME_Southern_Oranna_Path_OnLoad
    ]
}

script ISLANDGAME_Southern_Oranna_Path_OnLoad {
    setvar(VAR_TEMP_0, 1)
    if (flag(FLAG_SEEN_ABSOL)) {
        setvar(VAR_TEMP_1, 1)
    }
}


// Youngster Tyler
script ISLANDGAME_Southern_Oranna_Path_TrainerBattle1 {
    trainerbattle_single(TRAINER_S_ORANNA_PATH_TRAINER_1, TrainerBattle1_Text, TrainerBattle1_Defeated_Text)
    msgbox(TrainerBattle1_PostDefeated_Text, MSGBOX_AUTOCLOSE)
}

text TrainerBattle1_Text {
    format("{COLOR DARK_GRAY}Hey, you look like a newbie! You'll be easy pickings for a veteran like me!")
}

text TrainerBattle1_Defeated_Text
{
    "N-No way... I lost to a newbie!?"
}

text TrainerBattle1_PostDefeated_Text
{
    format("{COLOR DARK_GRAY}Okay... I lied about being a veteran. I'm also just a newbie too.")
}


// Lass Julia
script ISLANDGAME_Southern_Oranna_Path_TrainerBattle2 {
    trainerbattle_single(TRAINER_S_ORANNA_PATH_TRAINER_2, TrainerBattle2_Text, TrainerBattle2_Defeated_Text)
    msgbox(TrainerBattle2_PostDefeated_Text, MSGBOX_AUTOCLOSE)
}

text TrainerBattle2_Text {
    format("{COLOR DARK_GRAY}I caught some cute Pokémon. Wanna see them? They're pretty strong too!")
}

text TrainerBattle2_Defeated_Text
{
    "Well shoot! Your Pokémon are cute and strong too!"
}

text TrainerBattle2_PostDefeated_Text
{
    format("{COLOR DARK_GRAY}Where'd you get your Pokémon? I'd love to have them too!")
}



// Bug Catcher Manny
script ISLANDGAME_Southern_Oranna_Path_TrainerBattle3 {
    trainerbattle_single(TRAINER_S_ORANNA_PATH_TRAINER_3, TrainerBattle3_Text, TrainerBattle3_Defeated_Text)
    msgbox(TrainerBattle3_PostDefeated_Text, MSGBOX_AUTOCLOSE)
}

text TrainerBattle3_Text {
    format("{COLOR DARK_GRAY}My Pokémon is really rare! You don't stand a chance!")
}

text TrainerBattle3_Defeated_Text
{
    "How did you beat me? Your Pokémon must \n"
    "be even rarer!"
}

text TrainerBattle3_PostDefeated_Text
{
    format("{COLOR DARK_GRAY}Hmmm, I guess just because a Pokémon is rare, doesn't mean it's strong...")
}



// Youngster Logan
script ISLANDGAME_Southern_Oranna_Path_TrainerBattle4 {
    trainerbattle_single(TRAINER_S_ORANNA_PATH_TRAINER_4, TrainerBattle4_Text, TrainerBattle4_Defeated_Text)
    msgbox(TrainerBattle4_PostDefeated_Text, MSGBOX_AUTOCLOSE)
}

text TrainerBattle4_Text {
    format("{COLOR DARK_GRAY}I'm on a crazy winning streak right now! How'd you like to be my 10th victory?")
}

text TrainerBattle4_Defeated_Text
{
    "Whelp... Looks like we're back to zero."
}

text TrainerBattle4_PostDefeated_Text
{
    format("{COLOR DARK_GRAY}You ruined my perfect record today... ):")
}










script ISLANDGAME_Southern_Oranna_Path_EventScript_NPC1 {
    lock
    faceplayer
    msgbox(ISLANDGAME_Southern_Oranna_Path_NPC1Talk)
    release
}

text ISLANDGAME_Southern_Oranna_Path_NPC1Talk {
    format(
        "{COLOR DARK_GRAY}Have you seen any Jigglypuff yet? I like that Pokémon."
    )
}

script ISLANDGAME_Southern_Oranna_Path_EventScript_NPC2 {
    lock
    faceplayer
    if(!flag(FLAG_S_ORANNA_PATH_NPC)) {
        msgbox(ISLANDGAME_Southern_Oranna_Path_NPC2Talk1)
        checkitemspace(ITEM_POTION)
        if (!var(VAR_RESULT)) {
            msgbox("Oh... you don't have enough room for my gift.")
            goto(FiredEmployee_End)
        }
        giveitem(ITEM_POTION)
        setflag(FLAG_S_ORANNA_PATH_NPC)
    }
    msgbox(ISLANDGAME_Southern_Oranna_Path_NPC2Talk2)

    FiredEmployee_End:
    release
}

text ISLANDGAME_Southern_Oranna_Path_NPC2Talk1 {
    format(
        "{COLOR DARK_GRAY}You're a Pokémon Trainer, right? You could probably use this."
    )
}

text ISLANDGAME_Southern_Oranna_Path_NPC2Talk2 {
    format(
        "{COLOR DARK_GRAY}I used to work part-time at the Poké Mart in Cheriport, but I got fired. \p"
        "{COLOR DARK_GRAY}I'm fine with it though, my manager was a real jerk anyways."
    )
}

script ISLANDGAME_Southern_Oranna_Path_EventScript_SIGN {
    lock
    msgbox(ISLANDGAME_Southern_Oranna_Path_SIGN_DESC)
    release
}

text ISLANDGAME_Southern_Oranna_Path_SIGN_DESC {
    format(
        "{COLOR DARK_GRAY}<-- CHERIPORT\n"
        "{COLOR DARK_GRAY}CAMP PERSI -->"
    )
}

//seen absol event
const ABSOL_OW_ID = 9

movement SOP_WalkRight {
    walk_right
    step_end
}

movement SOP_MiddleWalkBottom {
    walk_down
    step_end
}

script ISLANDGAME_Southern_Oranna_Path_EventScript_TriggerTop {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, SOP_WalkRight)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, SOP_MiddleWalkBottom)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, SOP_MiddleWalkBottom)
    waitmovement(0)
    goto(ISLANDGAME_Southern_Oranna_Path_EventScript_SeenAbsol)
}

script ISLANDGAME_Southern_Oranna_Path_EventScript_TriggerMiddle {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, SOP_WalkRight)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, SOP_MiddleWalkBottom)
    waitmovement(0)
    goto(ISLANDGAME_Southern_Oranna_Path_EventScript_SeenAbsol)
}

script ISLANDGAME_Southern_Oranna_Path_EventScript_TriggerBottom {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, SOP_WalkRight)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER,  Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    goto(ISLANDGAME_Southern_Oranna_Path_EventScript_SeenAbsol)
}

movement SOP_Absol_Surprised {
    emote_exclamation_mark
    face_player
    walk_up
    delay_16 * 6
    step_end
}

movement SOP_Absol_Run_1 {
    walk_fast_right
    step_end
}

movement SOP_Absol_Jump {
    jump_right
    step_end
}

movement SOP_Absol_Run_2 {
    walk_fast_right * 3
    step_end
}

movement SOP_Absol_Run_3 {
    walk_fast_up * 4
    walk_fast_right * 2
    step_end
}

script ISLANDGAME_Southern_Oranna_Path_EventScript_SeenAbsol {
    setvar(VAR_TEMP_1, 1)
    setflag(FLAG_SEEN_ABSOL)
    playmoncry(SPECIES_ABSOL, CRY_MODE_ENCOUNTER)
    applymovement(ABSOL_OW_ID, SOP_Absol_Surprised)
    waitmovement(ABSOL_OW_ID)
    //maybe a message here?
    applymovement(ABSOL_OW_ID, SOP_Absol_Run_1)
    waitmovement(ABSOL_OW_ID)
    playse(SE_LEDGE)
    applymovement(ABSOL_OW_ID, SOP_Absol_Jump)
    waitmovement(ABSOL_OW_ID)
    playse(SE_LEDGE)
    applymovement(ABSOL_OW_ID, SOP_Absol_Jump)
    waitmovement(ABSOL_OW_ID)
    applymovement(ABSOL_OW_ID, SOP_Absol_Run_2)
    waitmovement(ABSOL_OW_ID)

    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    applymovement(ABSOL_OW_ID, SOP_Absol_Run_3)
    waitmovement(ABSOL_OW_ID)

    hideobjectat(ABSOL_OW_ID, MAP_ISLANDGAME_SOUTHERN_ORANNA_PATH)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
    waitmovement(0)
    releaseall
}