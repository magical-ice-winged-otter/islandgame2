const FLAG_HIDE_THIEF = FLAG_TEMP_1

const LOCALID_ARTICUNO = 2
const LOCALID_THIEF = 3
const LOCALID_AZURILL = 4
const LOCALID_ARTHUR = 5

mapscripts ISLANDGAME_MT_AURORA_2F_3_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        // see ISLANDGAME_ROSEVALE scripts.pory for state info
        if (var(VAR_ROSEVALE_STATE) != 1 && var(VAR_ROSEVALE_STATE) != 5) {
            setflag(FLAG_HIDE_THIEF)
        }
    }
}

movement WalkInPlaceUp {
    walk_in_place_up
}

movement JumpInPlaceDown2 {
    jump_in_place_down * 2
}

script ISLANDGAME_MT_AURORA_2F_3_Trigger_ThiefCutscene {
    lock
    playbgm(MUS_ENCOUNTER_AQUA, FALSE)

    // thief trash talks arthur
    applymovement(LOCALID_THIEF, Common_Movement_WalkInPlaceDown)
    applymovement(LOCALID_THIEF, Common_Movement_Delay32)
    waitmovement(0)
    msgbox(format(
        "{COLOR GREEN}Thief: {COLOR DARK_GRAY}Pfft. THAT was your attempt at battling? You didn't even land a single hit on any of my Pokémon! \p"
        "{COLOR DARK_GRAY}And you came ALL this way just to try and stop me? How pathetic."
    ))
    closemessage

    playmoncry(SPECIES_AZURILL, CRY_MODE_WEAK)
    applymovement(LOCALID_AZURILL, JumpInPlaceDown2)
    applymovement(LOCALID_THIEF, Common_Movement_Delay48)
    waitmovement(0)
    applymovement(LOCALID_ARTHUR, WalkInPlaceUp)
    applymovement(LOCALID_ARTHUR, Common_Movement_Delay32)
    waitmovement(0)
    msgbox(format("{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Mgrgrgr..."))


    msgbox(format(
        "{COLOR GREEN}Thief: {COLOR DARK_GRAY}Haha, this shiny Azurill is gonna snag me a fortune on the black market!\p"
        "As for you...{PAUSE 60} quit moping and just and get a new one for your little sis.\p" 
        "She's just a dumb child, she probably won't even notice the difference.\p"
        "Country bumpkins like you don't understand the value of shiny Pokémon."
    ))
    closemessage


    // notice player
    applymovement(LOCALID_THIEF, Common_Movement_QuestionMark)
    applymovement(LOCALID_ARTHUR, Common_Movement_Delay48)
    applymovement(LOCALID_THIEF, Common_Movement_WalkInPlaceDown)
    applymovement(LOCALID_THIEF, Common_Movement_Delay32)
    waitmovement(0)

    msgbox(format(
        "{COLOR DARK_GRAY}Hm?{PAUSE 60} And just who are you? A friend of his?\p"
        "{COLOR DARK_GRAY}Beat it, kid! Unless you want to get demolished like this loser!"
    ))
    closemessage

    applymovement(LOCALID_ARTHUR, Common_Movement_WalkInPlaceDown)
    applymovement(LOCALID_ARTHUR, Common_Movement_Delay32)
    waitmovement(0)
    msgbox(format("{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Please! You have to stop her! That Azurill means everything to my little sister!\pI-I tried my best...{PAUSE 60} but it just wasn't enough..."))
    closemessage
    fadedefaultbgm
    setvar(VAR_ROSEVALE_STATE, 5)
    applymovement(LOCALID_ARTHUR, Common_Movement_Delay48)
    waitmovement(0)
    release
}

script ISLANDGAME_MT_AURORA_2F_3_Arthur_Interact {
    lock
    faceplayer
    msgbox(format("{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Please! You have to stop her! That Azurill means everything to my little sister!\pI-I tried my best...{PAUSE 60} but it just wasn't enough..."))
    release
}

script ISLANDGAME_MT_AURORA_2F_3_Azurill_Interact {
    lock
    faceplayer
    playmoncry(SPECIES_AZURILL, CRY_MODE_WEAK)
    msgbox(format("Azu... {COLOR BLUE}(pls saef me frum meen ladi)"))
    release
}

script ISLANDGAME_MT_AURORA_2F_3_EdeaInteract {
    lock
    faceplayer
    trainerbattle_single(TRAINER_MT_AURORA_EDEA, 
        format("{COLOR GREEN}Thief: {COLOR DARK_GRAY}-sigh- This is why I hate kids.{PAUSE 60} You brats just never listen!")
        , "I-Impossible! Just who are you!?", ThiefDefeated)
}

movement Movement_AzurillOutOfWay {
    face_right
    lock_facing_direction
    walk_left
    unlock_facing_direction
}

movement Movement_ThiefRunAway {
    delay_16
    walk_faster_left
    walk_faster_down * 7
}

movement Movement_AzurillHappy {
    jump_in_place_right * 2
    walk_fast_right
    walk_fast_down * 2
    face_right
}

movement Movement_ArticunoDescend {
	walk_slow_down * 4
}

movement Movement_ArticunoAscend {
    lock_facing_direction
	walk_slow_up * 4
}

script(local) ThiefDefeated {
    lock
    
    // thief refuses to surrender mon
    applymovement(LOCALID_ARTHUR, Common_Movement_FaceUp)
    applymovement(LOCALID_ARTHUR, WalkInPlaceUp)
    applymovement(LOCALID_ARTHUR, Common_Movement_Delay32)
    waitmovement(0)
    msgbox(format(
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Ha! You lost!{PAUSE 60} Now, give me back my sister's Azurill!"
    ))
    applymovement(LOCALID_THIEF, Common_Movement_FaceDown)
    msgbox(format(
        "{COLOR GREEN}Edea: {COLOR DARK_GRAY}In your dreams, squirt! I'm not giving this Pokémon up! It's worth more than your dinky little inn!\p"
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}H-Huh? But you have to! We beat you, fair and square!\p"
        "{COLOR GREEN}Edea: {COLOR DARK_GRAY}And? What are you gonna do, tell on me to the Pokémon gods? I'm taking this Azurill with me! Peace out, losers!\p"
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}W-Wait!"
    ))
    closemessage
    playmoncry(SPECIES_ARTICUNO, CRY_MODE_NORMAL)
    waitmoncry

    applymovement(LOCALID_THIEF, Common_Movement_QuestionMark)
    applymovement(LOCALID_ARTHUR, Common_Movement_QuestionMark)
    applymovement(LOCALID_AZURILL, Common_Movement_QuestionMark)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_QuestionMark)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay32)
    waitmovement(0)
    msgbox(format("{COLOR RED}(Huh? What was that sound?)"))
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement(0)

    // articuno swoops down
    fadescreen(FADE_TO_WHITE)
    addobject(LOCALID_ARTICUNO)
    setobjectxy(LOCALID_ARTICUNO, 12, 9)
    fadescreen(FADE_FROM_WHITE)
    applymovement(LOCALID_ARTICUNO, Movement_ArticunoDescend)
    waitmovement(0)

    // articuno scares thief and they run
    playmoncry(SPECIES_ARTICUNO, CRY_MODE_NORMAL)
    msgbox(format("Cuuuuun!"))
    closemessage
    applymovement(LOCALID_THIEF, Common_Movement_FaceUp)
    waitmovement(0)
    applymovement(LOCALID_AZURILL, Common_Movement_FaceUp)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_THIEF, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCALID_ARTHUR, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCALID_AZURILL, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
    waitmovement(0)
    applymovement(LOCALID_THIEF, Common_Movement_WalkInPlaceFasterUp)    
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Edea: {COLOR DARK_GRAY}W-What!? Articuno!? Where in the blazes did you come from!?\p"
        "Ngggh...{PAUSE 60} F-Fine! I'll give back their stupid Azurill!\p"
        "You punks got lucky this time! Now... if you don't mind me...{PAUSE 60} I'm outta here!"
    ))
    closemessage

    applymovement(LOCALID_AZURILL, Movement_AzurillOutOfWay)
    applymovement(LOCALID_THIEF, Movement_ThiefRunAway)
    waitmovement(0)
    applymovement(LOCALID_ARTHUR, Common_Movement_FaceDown)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    waitmovement(0)
    applymovement(LOCALID_AZURILL, Common_Movement_FaceDown)
    waitmovement(0)
    removeobject(LOCALID_THIEF)
    waitmovement(0)
    playmoncry(SPECIES_ARTICUNO, CRY_MODE_NORMAL)
    msgbox(format("Cuuuuun..."))
    closemessage
    applymovement(LOCALID_ARTHUR, Common_Movement_FaceUp)
    waitmovement(0)
    applymovement(LOCALID_AZURILL, Common_Movement_FaceUp)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)

    // articuno leaves
    delay(30)
    applymovement(LOCALID_ARTICUNO, Movement_ArticunoAscend)
    waitmovement(0)
    fadescreen(FADE_TO_WHITE)
    removeobject(LOCALID_ARTICUNO)
    fadescreen(FADE_FROM_WHITE)

    applymovement(LOCALID_ARTHUR, WalkInPlaceUp)
    waitmovement(0)
    applymovement(LOCALID_ARTHUR, Common_Movement_Delay32)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    msgbox(format(
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Holy heck...{PAUSE 60} Was that Articuno in the flesh just now!?\p"
        "It left before we could say anything...{PAUSE 60} T-Thank you for the help, Articuno!"
    ))

    playmoncry(SPECIES_AZURILL, CRY_MODE_NORMAL)
    applymovement(LOCALID_AZURILL, Movement_AzurillHappy)
    waitmovement(0)
    applymovement(LOCALID_ARTHUR, Common_Movement_FaceLeft)
    msgbox(format(
        "Azurill! I'm so sorry for not being more watchful. I should've never left you and my little sister out of sight."
    ))
    applymovement(LOCALID_ARTHUR, Common_Movement_FaceUp)
    waitmovement(0)
    msgbox(format(
        "Oh, and thank you too! Er..."
    ))
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}{PLAYER}? Well, I owe you big and Articuno big time for helping me.\p"
        "I couldn't do anything... If it weren't for you and Articuno, Azurill would've been lost forever!\p"
        "Ah... Ah... Achoo!{PAUSE 60} Brrr... It's freezing in here.{PAUSE 60} Let's head back to Rosevale."
    ))
    setvar(VAR_ROSEVALE_STATE, 2) // see ISLANDGAME_ROSEVALE scripts.pory for state info
    
    fadescreen(FADE_TO_BLACK)
    delay(30)
    removeobject(LOCALID_ARTHUR)

    // warp back to rosevale
    warp(MAP_ISLANDGAME_ROSEVALE, 13, 26)
}