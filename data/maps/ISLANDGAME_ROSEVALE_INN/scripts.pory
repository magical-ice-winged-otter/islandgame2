const LOCALID_ARTHUR_SISTER = 2
const LOCALID_ARTHUR_DAD = 3
const LOCALID_ARTHUR_MOM = 1
const LOCALID_ARTHUR = 4
const LOCALID_MARILL = 5

mapscripts ISLANDGAME_ROSEVALE_INN_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        switch (var(VAR_ROSEVALE_STATE)) {
            case 3:
                // do nothing
                break
            default: 
                // post arthur fight
                setobjectxy(LOCALID_ARTHUR_DAD, 26, 4)
                setobjectxy(LOCALID_ARTHUR, 23, 5)
        }
    }
}

script ISLANDGAME_ROSEVALE_INN_Trigger_Cutscene {
    lock
    applymovement(LOCALID_ARTHUR, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    applymovement(LOCALID_ARTHUR, Common_Movement_Delay48)
    waitmovement(0)

    msgbox(format("{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Why can't I take on the Marya Gym Challenge? I want to become stronger!"))
    closemessage

    applymovement(LOCALID_ARTHUR_DAD, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    applymovement(LOCALID_ARTHUR_DAD, Common_Movement_Delay48)
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}Because you're not ready yet.\p"
        "Taking part in the Gym Challenge means you'll be traveling around the isles by yourself, and I don't trust you to not get into trouble."
    ))

    applymovement(LOCALID_ARTHUR, Common_Movement_WalkInPlaceRight)

    msgbox(format(
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Why not? I'm not a kid anymore! I can handle myself just fine!\p"
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}You not only ran into the caves of Mt. Aurora, which I have repeatedly told you not to do, but you also chased after a potentially dangerous criminal too.\p"
        "That shows me that you can't handle yourself at all.\p"
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}But... she stole Annie's Azurill! Are you saying I should've just let her get away!?\p"
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}And could you have gotten it back without {PLAYER}'s and Articuno's help?\p"
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}Mmmmmn...\p"
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}Do you realize what could've happened to you had they not been there?"
        "And what happens the next time you do something as reckless as that?\p"
        "I know your heart is in the right place, Arthur, but it's still too early for you."
    ))
    closemessage

    applymovement(LOCALID_ARTHUR, Common_Movement_Delay48)
    waitmovement(0)
    setvar(VAR_ROSEVALE_STATE, 3)
}

script ISLANDGAME_ROSEVALE_INN_Interact_ArthurMom {
    lock
    faceplayer
    switch (var(VAR_ROSEVALE_STATE)) {
        case 3: // pre arthur fight
            msgbox(format(
                "{COLOR GREEN}Arthur's Mom: {COLOR DARK_GRAY}Arthur and his father have been arguing about participating in the Marya Gym Challenge all morning.\pI understand that it's natural for a Pokémon trainer to eventually travel off all on their own, but I'm still worried if he'll be okay..."
            ))
            closemessage
        default: // post arthur fight
            msgbox(format(
                "{COLOR GREEN}Arthur's Mom: {COLOR DARK_GRAY}If you ever need a place to stay, our doors are always open."
            ), MSGBOX_NPC)
    }
    release
}

script ISLANDGAME_ROSEVALE_INN_Interact_ArthurSister {
    lock
    faceplayer
    if(!flag(FLAG_ROSEVALE_AMULET_COIN_GIFT)) {
        msgbox(format(
            "{COLOR GREEN}Annie: {COLOR DARK_GRAY}Hewwo! Do you have room to take my gift now?"
        ))
        checkitemspace(ITEM_AMULET_COIN)
        if(!var(VAR_RESULT)) {
            msgbox(format("Eeh? You still don't have space!"))
        }
        else {
            msgbox(format("Hooway! Here you go!"))
            giveitem(ITEM_AMULET_COIN)
            setflag(FLAG_ROSEVALE_AMULET_COIN_GIFT)
        }
    }
    else {
        msgbox(format("{COLOR GREEN}Annie: {COLOR DARK_GRAY}Me and A-zoo-wil are dancing! Wanna join?"), MSGBOX_NPC)
    }
    release
}

script ISLANDGAME_ROSEVALE_INN_Interact_Marill {
    lock
    faceplayer
    playmoncry(SPECIES_AZURILL,CRY_MODE_NORMAL)
    msgbox(format("Azu! {COLOR BLUE}(Good luck on the rest of your journey! I'll never forget you!)"), MSGBOX_NPC)
    release
}

script ISLANDGAME_ROSEVALE_INN_Interact_Arthur {
    lock
    switch (var(VAR_ROSEVALE_STATE)) {
        case 3: // pre arthur fight
            msgbox(format(
                "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}I-It's not fair..."
            ))
            closemessage
        default: // post arthur fight
            faceplayer
            msgbox(format("{COLOR GREEN}Arthur: {COLOR DARK_GRAY}{PLAYER}! I promise the next time we fight, I'll be much stronger!"), MSGBOX_NPC)
    }
    release
}

script ISLANDGAME_ROSEVALE_INN_Interact_ArthurDad {
    switch (var(VAR_ROSEVALE_STATE)) {
        case 3: // pre arthur fight
            msgbox(format(
                "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}You have to prove to me that you are responsible enough first."
            ))
            closemessage
        default: // post arthur fight
            faceplayer
            msgbox(format("{COLOR GREEN}Arthur's Dad: You're a great influence on Arthur, {PLAYER}.\pI'm glad that you met each other."), MSGBOX_NPC)
    }
}



script ISLANDGAME_ROSEVALE_INN_Interact_LockedDoor {
    lock
    msgbox(format(
        "Oi, the sign says “DO NOT DISTURB”. What part of that do you not get!?"
    ))
    release
}