// 0 = Magikarp
// 1 = Wishiwashi
// 2 = Slowpoke
// 3 = Shellder
const CATCH_OF_THE_DAY = VAR_TEMP_A


mapscripts ISLANDGAME_VERDANT_PATH_FISHHOUSE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        // handle Catch of the Day random Pokemon
        if (!flag(FLAG_DAILY_VERDANT_PATH_ARNIE_ROLL)) {
            random(100)
            copyvar(VAR_VERDANT_PATH_STATE, VAR_RESULT)
            setflag(FLAG_DAILY_VERDANT_PATH_ARNIE_ROLL)

            if (var(VAR_VERDANT_PATH_STATE) < 50) {
                setvar(CATCH_OF_THE_DAY, 0)
                copyvar(VAR_VERDANT_PATH_STATE, 0)
            }
            elif (var(VAR_VERDANT_PATH_STATE) < 50 + 30) {
                setvar(CATCH_OF_THE_DAY, 1)
                copyvar(VAR_VERDANT_PATH_STATE, 1)
            }
            elif (var(VAR_VERDANT_PATH_STATE) < 50 + 30 + 15) {
                setvar(CATCH_OF_THE_DAY, 2)
                copyvar(VAR_VERDANT_PATH_STATE, 2)
            }
            else {
                setvar(CATCH_OF_THE_DAY, 3)
                copyvar(VAR_VERDANT_PATH_STATE, 3)
            }
        }
        else {
            setvar(CATCH_OF_THE_DAY, VAR_VERDANT_PATH_STATE)
        }

    }
}


script ISLANDGAME_VERDANT_PATH_FISHHOUSE_EventScript_Arnie {
    lock
    faceplayer
    goto_if_set(FLAG_VERDANT_PATH_ROD_RECEIVED, ISLANDGAME_VERDANT_PATH_FISHHOUSE_RodReceived) // You are part of club now
    showmoneybox(0,0)
    msgbox(ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieIntro, MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, NO, ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieDecline)
    checkmoney(2500)
    goto_if_eq(VAR_RESULT, FALSE, ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieBroke)
    removemoney(2500)
    updatemoneybox
    msgbox(ISLANDGAME_VERDANT_PATH_FISHHOUSE_JoinClub)
    hidemoneybox
    giveitem(ITEM_SUPER_ROD)
    setflag(FLAG_VERDANT_PATH_ROD_RECEIVED)
    msgbox(ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieOutro)
    goto(ISLANDGAME_VERDANT_PATH_FISHHOUSE_RodReceived)
    release
}

script ISLANDGAME_VERDANT_PATH_FISHHOUSE_RodReceived {
    goto(ArnieServices)

    ArnieServices:
        msgbox(format(
            "How's it hangin'?"
        ))
        dynmultipush("Catch of the Day", 1)
        dynmultipush("Fishing Quests", 2)
        dynmultipush("Trading", 3)
        dynmultipush("How to fish", 4)
        dynmultipush("Later, dude", 5)
        dynmultistack(0,0,TRUE,4,FALSE,0,DYN_MULTICHOICE_CB_NONE)

        switch (var(VAR_RESULT)) {
            case 1:
                goto(ArnieCatchOfTheDay)
            case 2:
                msgbox(format(
                    "blah"
                ))
                goto(ArnieServices)
            case 3:
                msgbox(format(
                    "This club is a great place to trade with other fellow fishing fanatics!\p"
                    "There's always new faces popping in and out, so be sure to check in often!"
                ))
                goto(ArnieServices)
            case 4:
                msgbox(format("The second you get a bite, you gotta reel it in only once so you don't break your line.\pThen keep at it every time 'till a Pokémon's on the line!\pIt takes a lot of practice and a little bit of luck, but with enough time, you'll become a master baiter!"))                    
                goto(ArnieServices)
            default:
                goto(ArnieServices_End)
        }

    ArnieCatchOfTheDay:
        showmoneybox(17, 1)
        switch(var(CATCH_OF_THE_DAY)) {
            case 1: // Wishiwashi
                msgbox(format(
                    "Our catch of the day is known as “the demon of the sea” in Alola and Galar...\p"
                    "The small fry Pokémon, Wishiwashi!\p"
                    "Don't let its size fool you, though, for when they come together into their School Form, a single water gun is more powerful than a hydro pump!\p"
                    "For just ¥1000, you too can get a taste of this lil sea demon!"
                ), MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES) {
                    checkmoney(1000)
                    if (!var(VAR_RESULT)) {
                        msgbox(format(
                            "Sorry, ya don't got enough clams."
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                    else {
                        removemoney(1000)
                        updatemoneybox
                        playse(SE_PIN)
                        waitse
                        givemon(SPECIES_WISHIWASHI, 5)
                        playfanfare(MUS_OBTAIN_ITEM)
                        bufferspeciesname(STR_VAR_1, SPECIES_WISHIWASHI)
                        msgbox(format("You received a Wishiwashi!"))
                        waitfanfare 
                        msgbox(format(
                            "Enjoy the fresh catch!"
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                }
                else {
                    hidemoneybox
                    goto(ArnieServices)
                }
            case 2: // Slowpoke
                msgbox(format(
                    "Our catch of the day is a fisherman's best bud, the dopey Pokémon Slowpoke!\p"
                    "Its tail not only serves as a tasty treat, but also a lure for fishing too!\p"
                    "For ¥1500, this absentminded animal is all yours!"
                ), MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES) {
                    checkmoney(1500)
                    if (!var(VAR_RESULT)) {
                        msgbox(format(
                            "Sorry, ya don't got enough clams."
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                    else {
                        removemoney(1500)
                        updatemoneybox
                        playse(SE_PIN)
                        waitse
                        givemon(SPECIES_SLOWPOKE, 5,,,,2)
                        playfanfare(MUS_OBTAIN_ITEM)
                        bufferspeciesname(STR_VAR_1, SPECIES_SLOWPOKE)
                        msgbox(format("You received a Slowpoke!"))
                        waitfanfare 
                        msgbox(format(
                            "Enjoy the fresh catch!"
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                }
                else {
                    hidemoneybox
                    goto(ArnieServices)
                }
            case 3: // Shellder
                msgbox(format(
                    "Our catch of the day is courtesy of Tidalcove's sandy shore...\p"
                    "Shellder, the Bivalve Pokémon!\p"
                    "Its tender, soft body is enveloped by diamond-hard shell, said to have once been used by our ancestors to craft shields!\p"
                    "For ¥2,000, this tiny tanky mollusk is all yours for the taking!"
                ), MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES) {
                    checkmoney(2000)
                    if (!var(VAR_RESULT)) {
                        msgbox(format(
                            "Sorry, ya don't got enough clams."
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                    else {
                        removemoney(2000)
                        updatemoneybox
                        playse(SE_PIN)
                        waitse
                        givemon(SPECIES_SHELLDER,5,,,,1,,,,,,,,,,,,,,MOVE_ICICLE_SPEAR,MOVE_ROCK_BLAST,MOVE_RAPID_SPIN, MOVE_TWINEEDLE)
                        playfanfare(MUS_OBTAIN_ITEM)
                        bufferspeciesname(STR_VAR_1, SPECIES_SHELLDER)
                        msgbox(format("You received a Shellder!"))
                        waitfanfare 
                        msgbox(format(
                            "Enjoy the fresh catch!"
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                }
                else {
                    hidemoneybox
                    goto(ArnieServices)
                }
            default: // Magikarp
                msgbox(format(
                    "Our catch of the day is none other than the king of losers himself, Magikarp!\p"
                    "For the low-low price of ¥500, you too can wear the crown of loserdom!"
                ), MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES) {
                    checkmoney(500)
                    if (!var(VAR_RESULT)) {
                        msgbox(format(
                            "Sorry, ya don't got enough clams."
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                    else {
                        removemoney(500)
                        updatemoneybox
                        playse(SE_PIN)
                        waitse
                        givemon(SPECIES_MAGIKARP, 5)
                        playfanfare(MUS_OBTAIN_ITEM)
                        bufferspeciesname(STR_VAR_1, SPECIES_MAGIKARP)
                        msgbox(format("You received a Magikarp!"))
                        waitfanfare 
                        msgbox(format(
                            "Enjoy the fresh catch!"
                        ))
                        hidemoneybox
                        goto(ArnieServices)
                    }
                }
                else {
                    hidemoneybox
                    goto(ArnieServices)
                }
        }

    ArnieServices_End:
        msgbox(format(
            "Tight lines!"
        ))
        release
}

text ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieIntro {
    format(
        "{COLOR DARK_GRAY}Howdy there, trainer! I'm Arnie, renowned fishing guru and president of the Fishing Fanatics! \p"
        "{COLOR DARK_GRAY}Would you like to join us? It's only ¥2500 for a membership!"
    )
}

script ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieDecline {
    msgbox(format("{COLOR DARK_GRAY}Well, if you ever change your mind, I'll always be here!"))
    hidemoneybox
    release
}

script ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieBroke {
    msgbox(format("{COLOR DARK_GRAY}Sorry, I can't sell them for any less than 2500¥."))
    hidemoneybox
    release
}


text ISLANDGAME_VERDANT_PATH_FISHHOUSE_JoinClub {
    format(
        "{COLOR DARK_GRAY}Congrats! You're a Fishing Fanatic now! Here's a little welcome gift from me."
    )
}

text ISLANDGAME_VERDANT_PATH_FISHHOUSE_ArnieOutro {
    format(
        "{COLOR DARK_GRAY}That right there is a custom-made fishing rod hand-crafted by yours truly. \p"
        "{COLOR DARK_GRAY}It's proof of your admission to our AWEsome club!"
    )
}

script ISLANDGAME_VERDANT_PATH_FISHHOUSE_EventScript_Trade {
    lock
    faceplayer
    goto_if_set(FLAG_VERDANT_PATH_TRADE_DONE, ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeFinished)
    setvar(VAR_0x8008, INGAME_TRADE_STARYU)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
    msgbox(ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeTalk)
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
    goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeWrong)
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
    goto_if_ne(VAR_RESULT, VAR_0x8009, ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeWrong)
    msgbox(format(
        "W-What!? Could it be...!? The LEGENDARY Gyarados in all of its glory!?"
        "T-Trainer! You MUST let me have it! H-Here, I'll even give you my prized Staryu for it!"
    ), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        goto(ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeDecline)
    }
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	bufferspeciesname(STR_VAR_1, VAR_0x8009)
	msgbox(format("Finally... I have a Gyarados all of my own... I can rest easy now, knowing that I have one of the rarest water Pokémon known to mankind!"))
	setflag(FLAG_VERDANT_PATH_TRADE_DONE)
    release
}

text ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeTalk {
    format(
        "Urgg... I'm convinced it doesn't exist!\p"
        "The legendary Gyarados... I've been searching for it for YEARS!\p"
        "But all I've found are these worthless Magikarps...\p"
        "I'd do anything to get my hands on a mythical beast like Gyarados. Heck, I'd even be willing to trade my SUPER SPECIAL Staryu for it!"
        )
}

script ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeFinished {
    msgbox(format("Finally... I have a Gyarados all of my own... I can rest easy now, knowing that I have one of the rarest water Pokémon known to mankind!"))
    release
}

script ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeWrong {
    msgbox(format("Unless you've a Gyarados to show me, go away!"))
    release
}

script ISLANDGAME_VERDANT_PATH_FISHHOUSE_TradeDecline {
    msgbox(format(
        "C-Come on, man! Do me a solid!" 
    ))
    release
}
script ISLANDGAME_VERDANT_PATH_FISHHOUSE_EventScript_NPC {
    lock
    faceplayer
    msgbox(ISLANDGAME_VERDANT_PATH_FISHHOUSE_NPCTalk)
    release
}

text ISLANDGAME_VERDANT_PATH_FISHHOUSE_NPCTalk {
    format(
        "{COLOR DARK_GRAY}Nothing beats Arnie's custom-made rods. They're positively remarkable."
        )
}