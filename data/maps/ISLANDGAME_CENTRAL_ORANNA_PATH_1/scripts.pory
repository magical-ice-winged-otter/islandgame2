const LOCALID_ENTRANCE_RANGER_1 = 1
const LOCALID_ENTRANCE_RANGER_2 = 2
const LOCALID_ENTRANCE_RANGER_3 = 3
const LOCALID_MIGHTYENA_1 = 4
const LOCALID_MIGHTYENA_2 = 5
const LOCALID_MIGHTYENA_3 = 6
const LOCALID_PKMN1 = 14
const LOCALID_PKMN2 = 15
const LOCALID_PKMN3 = 16

const POS_HIDDEN_X = 0
const POS_HIDDEN_Y = 55

const VAR_MIGHTYENA_HIDDEN = VAR_TEMP_0

const LOCAL_ID_ENCOUNTER_1 = 19
const LOCAL_ID_ENCOUNTER_2 = 20
const LOCAL_ID_ENCOUNTER_3 = 21

mapscripts ISLANDGAME_CENTRAL_ORANNA_PATH_1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setobjectaswildencounter(LOCAL_ID_ENCOUNTER_1, ENCOUNTER_LAND)
        setobjectaswildencounter(LOCAL_ID_ENCOUNTER_2, ENCOUNTER_LAND)
        setobjectaswildencounter(LOCAL_ID_ENCOUNTER_3, ENCOUNTER_LAND)

        switch (var(VAR_MINTY_MEADOWS_STATE)) {
            case 4:
                setvar(VAR_MIGHTYENA_HIDDEN, 1)
                setobjectxyperm(LOCALID_MIGHTYENA_1, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_MIGHTYENA_2, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_MIGHTYENA_3, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ENTRANCE_RANGER_1, 41, 38)
                setobjectxyperm(LOCALID_ENTRANCE_RANGER_3, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ENTRANCE_RANGER_2, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_PKMN1, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_PKMN2, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_PKMN3, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectmovementtype(LOCALID_ENTRANCE_RANGER_1, MOVEMENT_TYPE_FACE_RIGHT)
        }
    }
}

movement Movement_PlayerPushBack {
    walk_down
}

movement Movement_MightyenaGrowl {
    jump_in_place_down
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Trigger_MightyenaTooDangerous {
    lock
    playmoncry(SPECIES_MIGHTYENA, CRY_MODE_NORMAL)
    applymovement(LOCALID_MIGHTYENA_1, Movement_MightyenaGrowl)
    waitmovement(0)
    waitmoncry
    playse(SE_PIN)
    applymovement(1, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(1, Common_Movement_FacePlayer)
    waitmovement(0)
    applymovement(1, Common_Movement_Delay32)
    msgbox(format("Hey! It's too dangerous to go any further! Please let us deal with this!"))
    closemessage
    applymovement(1, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_PlayerPushBack)
    waitmovement(0)
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC1 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC1,
        format("Take a look at my darling Pokémon!"),
        "Oh dear..."
    )
    msgbox(format("Are you headed to Tidalcove? It's a fairly long journey ahead. Good luck!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC2 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC2,
        format("According to all known laws of aviation, there's no way that a non-flying type Bug Pokémon should be able to fly."),
        "The Bug Pokémon of course, flies \nanyways."
    )
    msgbox(format("Bug Pokémon don't care what humans think is impossible."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC3 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC3,
        format("My glasses! Where are my glasses? I can't see anything without my glasses!"),
        "Oh, here they are.{PAUSE 60} They were in my\n pocket the entire time."
    )
    msgbox(format("Sorry. I'm kinda forgetful. Umm... What was I doing again?\pI knew I came down here for a reason..."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC4 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC4,
        format("Argh! Not again! Why do these damn Ralts keep teleporting away!?\pTime to take my frustration on you!"),
        "Mrgrgrgr..."
    )
    msgbox(format("I'm sorry. It was rude of me to subject you to my anger."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_EntranceRanger {
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
        case 4: 
            faceplayer
            msgbox(
                format(
                    "Phew, we finally managed to get rid of that pack of wild Mightyena...\p"
                    "The leader of the pack was especially vicious and aggressive.\p"
                    "I've never seen anything like it, especially around these parts.\p"
                    "When it ran off, the other members of its pack followed suit.\p"
                    "I hope no one is in danger. Please be extra careful when traveling!"
                )
            )
        default: 
            faceplayer
            msgbox(format("Hey! It's too dangerous to go any further! Please let us deal with this!"))
            applymovement(1, Common_Movement_FaceOriginalDirection)
            waitmovement(0)
    }
    closemessage
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_EntranceSign {
    msgbox(format("CENTRAL ORANNA PATH\nMAPLEGROVE{RIGHT_ARROW}"), MSGBOX_SIGN)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_GrottoSign {
    msgbox(format("Hi! I'm a sentient sign. Below me is a hidden grotto!\pHidden grottoes can sometimes have items or even special Pokémon!\pDon't worry if you don't find anything. You can always check back tomorrow!\p- Billy The Board (Not a Pokémon)"), MSGBOX_SIGN)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_ExtraRanger1 {
    lock
    msgbox(format("Linoone! Use slash!"))
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_ExtraRanger2 {
    lock
    msgbox(format("Watch out, Ariados!"))
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_RouteMaster {
    lock
    faceplayer
    if(
        defeated(TRAINER_CORANNA_1_NPC1) &&
        defeated(TRAINER_CORANNA_1_NPC2) &&
        defeated(TRAINER_CORANNA_1_NPC3) &&
        defeated(TRAINER_CORANNA_1_NPC4) &&
        defeated(TRAINER_CORANNA_2_CAMPER) &&
        defeated(TRAINER_CORANNA_2_MANIAC) &&
        defeated(TRAINER_CORANNA_2_BIKER) &&
        defeated(TRAINER_CORANNA_2_FISHERMAN) && 
        defeated(TRAINER_MINTY_MEADOWS_5)  &&
        !defeated(TRAINER_CORANNA_PATHMASTER)
    ) {
        msgbox(format(
            "Wow! You beat every other trainer on this path!\p"
            "As a reward, I'll give you this ultra rare Petaya berry!\p"
            "...but only if you can beat me in a battle! Are you ready?"
        ), MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            trainerbattle_no_intro(TRAINER_CORANNA_PATHMASTER, "GG! You won!")
            msgbox(format(
                "Well, a deal's a deal! I pass this berry onto you, as well as the title of Central Oranna Path's Path Master!\p"
                "It's kind of mouthful, I know."
            ))
            giveitem(ITEM_PETAYA_BERRY)
            goto(Coranna_PathMaster_End)
        }
        else {
            msgbox(format(
                "Okay. Let me know when you are prepared!"
            ))
            goto(Coranna_PathMaster_End)
        }
    }
    if (defeated(TRAINER_CORANNA_PATHMASTER)) {
        msgbox(format(
            "With skills like that, I've no doubt you'll be ready to take on Ruka!"
        ))
    }
    else {
        msgbox(format(
            "Greetings, trainer! I am what's known as a Path Master!\p"
            "If you manage to defeat all the other trainers on this path, return to me!"
        ))
    }

    Coranna_PathMaster_End:
        closemessage
        release
}