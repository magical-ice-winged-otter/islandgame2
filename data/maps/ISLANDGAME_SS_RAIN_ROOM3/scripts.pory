const LOCALID_GENTLEMAN = 1
const ROOM3_OUT_WARP_X  = 19
const ROOM3_OUT_WARP_Y  = 11

mapscripts ISLANDGAME_SS_RAIN_ROOM3_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_TEMP_0, 0)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: SS_Rain_EventScript_InvadeRoom
    ]
}

script SS_Rain_EventScript_InvadeRoom {
    lockall
    setvar(VAR_TEMP_0, 1)
    if (flag(FLAG_LEFT_SHIP)) {
        releaseall
        return
    }
    applymovement(LOCALID_GENTLEMAN, ExclaimFacePlayer)
    waitmovement(0)
    msgbox(SS_Rain_InvadeRoomReaction_Text)
    waitmessage
    closemessage
    checkitem(ITEM_FRESH_WATER, 1)

    switch(var(VAR_RESULT)) {
        case 0: goto(SS_Rain_EventScript_HasNoWater) //not found           
        case 1: goto(SS_Rain_EventScript_HasWater) //found
    }
    releaseall
}

movement ExclaimFacePlayer {
    emote_exclamation_mark
    face_player
    step_end
}

movement WalkToPlayer {
    walk_left
    walk_up
    walk_up
    step_end
}
movement RunAway {
    walk_in_place_faster_up
    step_end
}

text SS_Rain_InvadeRoomReaction_Text {
    format("{COLOR RED}What are you doing in my room? Get out!")
}

script SS_Rain_EventScript_HasNoWater {
    applymovement(OBJ_EVENT_ID_PLAYER, RunAway)
    waitmovement(0)
    warpdoor(MAP_SS_RAIN_HALLWAY, ROOM3_OUT_WARP_X, ROOM3_OUT_WARP_Y)
}

script SS_Rain_EventScript_HasWater {
    msgbox(format("{COLOR RED}Huh?"))
    waitmessage
    applymovement(LOCALID_GENTLEMAN, WalkToPlayer)
    waitmovement(0)
    msgbox(format("{COLOR RED}He sent you to give me water?"))
    
    setflag(FLAG_ROOM3_GAVE_WATER)
    removeitem(ITEM_FRESH_WATER, 1)
    applymovement(LOCALID_GENTLEMAN, RunAway)
    waitmovement(0)
    msgbox(SS_Rain_CalmGentleman_Text)
    waitbuttonpress
    closemessage
}

script SS_Rain_EventScript_Talk {
    lockall
    faceplayer
    msgbox(SS_Rain_CalmGentleman_Text)
    releaseall
}

text SS_Rain_CalmGentleman_Text {
    format("{COLOR DARK_GRAY}I apologize for my rudeness. I don't take kindly to strangers entering my room without notice.")
}

script SS_Rain_EventScript_Item {
    finditem(ITEM_RARE_CANDY)
}

