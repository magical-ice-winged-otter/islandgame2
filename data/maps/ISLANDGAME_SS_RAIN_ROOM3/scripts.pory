const LOCALID_GENTLEMAN = 1
const ROOM3_OUT_WARP_X  = 19
const ROOM3_OUT_WARP_Y  = 8

mapscripts ISLANDGAME_SS_RAIN_ROOM3_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_TEMP_0, 0)
        if(!flag(FLAG_LEFT_SS_RAIN)) {
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: SS_Rain_EventScript_InvadeRoom
    ]
}

script SS_Rain_EventScript_InvadeRoom {
    lockall
    setvar(VAR_TEMP_0, 1)
    if (flag(FLAG_ROOM3_GAVE_WATER)) {
        releaseall
        return
    }
    playse(SE_PIN)
    applymovement(LOCALID_GENTLEMAN, ExclaimFacePlayer)
    waitmovement(0)
    msgbox(SS_Rain_InvadeRoomReaction_Text)
    waitmessage
    closemessage

    if(flag(FLAG_LEFT_SS_RAIN) && !flag(FLAG_ROOM3_GAVE_WATER)) {
        goto(SS_Rain_EventScript_HasNoWater)
    }

    checkitem(ITEM_FRESH_WATER, 1)


    switch(var(VAR_RESULT)) {
        case 0: goto(SS_Rain_EventScript_HasNoWater) //not found           
        case 1: goto(SS_Rain_EventScript_HasWater) //found
    }
    releaseall
}

movement ExclaimFacePlayer {
    emote_exclamation_mark
    face_player
    step_end
}

movement WalkToPlayer {
    walk_left
    walk_up
    walk_up
    step_end
}
movement RunAway {
    walk_in_place_faster_up
    step_end
}

text SS_Rain_InvadeRoomReaction_Text {
    format("{COLOR DARK_GRAY}Hey! What are you doing in my room? Get out!")
}

script SS_Rain_EventScript_HasNoWater {
    applymovement(OBJ_EVENT_ID_PLAYER, RunAway)
    waitmovement(0)
    warpdoor(MAP_SS_RAIN_HALLWAY, ROOM3_OUT_WARP_X, ROOM3_OUT_WARP_Y)
}

script SS_Rain_EventScript_HasWater {
    msgbox(format("{COLOR DARK_GRAY}Huh?"))
    waitmessage
    closemessage
    applymovement(LOCALID_GENTLEMAN, WalkToPlayer)
    waitmovement(0)
    msgbox(format("{COLOR DARK_GRAY}My nephew sent you to give me water?"))
    closemessage
    
    setflag(FLAG_ROOM3_GAVE_WATER)
    removeitem(ITEM_FRESH_WATER, 1)
    applymovement(LOCALID_GENTLEMAN, RunAway)
    waitmovement(0)
    msgbox(SS_Rain_CalmGentleman_Text)
    waitbuttonpress
    closemessage
}

script SS_Rain_EventScript_Talk {
    lockall
    faceplayer
    msgbox(SS_Rain_CalmGentleman_Text)
    releaseall
}

text SS_Rain_CalmGentleman_Text {
    format("{COLOR DARK_GRAY}I apologize for my rudeness. Please, make yourself at home.")
}

script ISLANDGAME_SS_RAIN_ROOM3_Boy {
    lock
    faceplayer
    msgbox(format(
        "I don't get many chances to see pristine seas like the ones here often."
    ))
    closemessage
    release
}