const FLAG_DAYTIME = FLAG_TEMP_2
const FLAG_NIGHTTIME = FLAG_TEMP_3

mapscripts ISLANDGAME_BELLEHAVEN_FOSTER_HOME_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if(var(VAR_VALLEY_OF_PEACE_STATE) == 2) { // cubone quest cutscene
            setflag(FLAG_NIGHTTIME)
            setflag(FLAG_DAYTIME)

            setobjectxyperm(8, 1, 2)
            setobjectmovementtype(8, MOVEMENT_TYPE_FACE_UP)
            addobject(8)
            setobjectxyperm(4, 18, 14)
            showobjectat(8, MAP_ISLANDGAME_BELLEHAVEN_FOSTER_HOME) 
            setobjectmovementtype(OBJ_EVENT_ID_PLAYER, MOVEMENT_TYPE_FACE_LEFT)
        }
        else {

        }
        gettime

        if (var(VAR_0x8000) < 6 || var(VAR_0x8000) >= 18) {
            setflag(FLAG_NIGHTTIME) // controls daytime npcs

            returnqueststate(QUEST_VALLEY_OF_PEACE_CUBONE) // Hides caretaker until cubone quest is active
            switch(var(VAR_RESULT)) {
                case 1: setflag(FLAG_TEMP_4)
                default:
            }

        }
        else {
            setflag(FLAG_DAYTIME) // controls nighttime npcs
        }
    }    
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_VALLEY_OF_PEACE_STATE, 2 {
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
            delay(30)
            applymovement(8, Common_Movement_WalkInPlaceFasterUp)
            waitmovement(0)
            playse(SE_M_RAIN_DANCE)
            waitse
            applymovement(8, ISLANDGAME_BELLEHAVEN_FOSTER_HOME_FrankieMovement1)
            waitmovement(0)
            playse(SE_SLIDING_DOOR)
            waitse
            applymovement(8, ISLANDGAME_BELLEHAVEN_FOSTER_HOME_FrankieMovement2)
            waitmovement(0)
            delay(30)
            msgbox(format(
                "Good grief...{PAUSE 30} this job's a real handful sometimes. I really do envy trainers like you.\p"
                "Getting to explore the world, meeting all kinds of people and Pokémon, not having to deal with boring chores and a bossy old man who'll never acknowledge all the work you do around here...\p"
                "Heh...{PAUSE 30} but I guess I can't complain too much. I chose to do this, after all."
            ))
            closemessage
            delay(30)
            applymovement(8, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            delay(30)
            msgbox(format(
                "I used to have a Vulpix of my own when I was younger. We were inseparable.\p"
                "But one night, she was taken from me by a group of poachers while we were taking a stroll down Aetheria, and that was the last time I ever saw her."
                "So I started working here, hoping that one day, I might see her again.\p"
                "The same thing happened to that poor Cubone's mother. I found him crying out for her after hearing that a group of poachers invaded the valley.\p"
                "Those rotten poachers...{PAUSE 30} It boils my blood just thinking about them...\p"
                "-sigh- But this is all I can do. Taking care of orphaned Pokémon and helping them reunite with their owners, or finding them a new home.\p"
            ))
            closemessage
            delay(30)
            applymovement(8, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            delay(30)
            msgbox(format(
                "Ah, it's getting late. {PAUSE 30}Thanks for lending an ear, {PLAYER}. You should go upstairs and rest."
            ))



            fadescreen(FADE_TO_BLACK)
            msgbox(format(
                "{COLOR RED}(I spent the rest of the night at the foster home.)"
            ))
            closemessage
            setvar(VAR_0x8000, 4)
            setvar(VAR_0x8001, 12)
            callnative(TeleportCamera)
            setobjectxy(OBJ_EVENT_ID_PLAYER, 4, 12)
            setobjectxy(8, 4, 10)
            // setobjectmovementtype(8, MOVEMENT_TYPE_FACE_DOWN)
            // addobject(8)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
            applymovement(8, Common_Movement_FaceDown)
            fadescreen(FADE_FROM_BLACK)



            delay(60)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
            waitmovement(0)
            delay(30)
            msgbox(format(
                "Ah, leaving already? That's a shame. It was nice chatting with someone my age.\p"
                "But I shouldn't hold you hostage here. I'm sure you've more important matters to attend to as a trainer.\p"
                "Feel free to stop by once in a while, if you've ever the time. Hehe."
            ))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
            waitmovement(0)
            delay(20)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
            waitmovement(0)
            playmoncry(SPECIES_CUBONE, CRY_MODE_NORMAL)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_QuestionMark)
            applymovement(8, Common_Movement_QuestionMark)
            waitmoncry

            setobjectxyperm(9, 7,7)
            addobject(9)
            showobjectat(9, MAP_ISLANDGAME_BELLEHAVEN_FOSTER_HOME) 
            applymovement(9, ISLANDGAME_BELLEHAVEN_FOSTER_HOME_CuboneMovement)
            delay(20)


            applymovement(8, Common_Movement_FaceRight)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
            waitmovement(9)
            applymovement(8, Common_Movement_FaceDown)
            waitmovement(0)
            delay(30)
            msgbox(format(
                "Cuu! {COLOR BLUE}(Hooman! Take Cubone with you! Please!)\p"
                "{COLOR DARK_GRAY}Cuu... {COLOR BLUE}(Hooman is powerful trainer. Trainer travel around Marya. Trainer might find Mama...)\p"
                "{COLOR DARK_GRAY}...Cuu? {COLOR BLUE}(...Can hooman help Cubone find Mama?)"
            ))
            closemessage
            applymovement(8, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            delay(20)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
            waitmovement(0)
            msgbox(format(
                "Haha, I see Cubone's taken quite a liking to you, {PLAYER}.\p"
                "I take it he'll be tagging along with you from now on?\p"
                "Normally I have to charge you an adoption fee... but I think I'll give your little friend an exception just this one time.\pJust don't tell Mr. Herriman, or he'll give me an earful about it!"
            ))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
            waitmovement(0)
            delay(30)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
            playse(SE_BALL)

            showobjectat(10, MAP_ISLANDGAME_BELLEHAVEN_FOSTER_HOME) 
            addobject(10) // show ball
            setobjectxy(10,5,13)

            waitse
            playse(SE_BALL_OPEN)
            waitse
            setobjectxy(9, 18, 14) // clear cubone

            giveitem(ITEM_ISLANDGAME_CUBONES_POKEBALL)
            startquest(QUEST_VALLEY_OF_PEACE_CUBONE)
            completesubquest(QUEST_VALLEY_OF_PEACE_CUBONE, SUB_QUEST_CUBONE_1)
            removeobject(10)
            setobjectxy(10, 18, 14) // clear pokeball
            applymovement(8, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            delay(30)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
            waitmovement(0)
            delay(30)
            msgbox(format(
                "Best of luck to you and your new friend, {PLAYER}!"
            ))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
            waitmovement(0)
            setvar(VAR_VALLEY_OF_PEACE_STATE, 3)
            warp(MAP_ISLANDGAME_BELLEHAVEN, 26, 21)
        }
    ]
}



movement ISLANDGAME_BELLEHAVEN_FOSTER_HOME_FrankieMovement1 {
    walk_right * 2
    face_up 
}

movement ISLANDGAME_BELLEHAVEN_FOSTER_HOME_FrankieMovement2 {
    walk_left
    walk_down * 3
    face_right
}

movement ISLANDGAME_BELLEHAVEN_FOSTER_HOME_CuboneMovement {
    walk_fast_down * 6
    walk_fast_left * 2
    jump_in_place_left * 2
}





script ISLANDGAME_BELLEHAVEN_FOSTER_HOME_PichuDay {
    lock
    faceplayer
    playmoncry(SPECIES_PICHU, CRY_MODE_NORMAL)
    msgbox(format(
        "I LIEK CEREAL!!!!!!!!!!"
    ))
    applymovement(6, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    closemessage
    release
}

script ISLANDGAME_BELLEHAVEN_FOSTER_HOME_PichuNight {
    lock
    faceplayer
    playmoncry(SPECIES_PICHU, CRY_MODE_NORMAL)
    msgbox(format(
        "Mmmmmm...{PAUSE 60} chocolat melk"
    ))
    applymovement(7, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    closemessage
    release
}

script ISLANDGAME_BELLEHAVEN_FOSTER_HOME_Happiny {
    lock
    faceplayer
    playmoncry(SPECIES_HAPPINY, CRY_MODE_NORMAL)
    msgbox(format(
        "COCO COCO COCO COCO!"
    ))
    closemessage
    release
}

script ISLANDGAME_BELLEHAVEN_FOSTER_HOME_MadameFoster {
    lock
    faceplayer
    if(!flag(FLAG_TEMP_3)) {
        msgbox(format(
            "Oh-ho-ho, these baby Pokémon are always so full of energy. I envy it."
        ))
    }
    else {
        msgbox(format(
            "Hello dearie! All of the Pokémon have gone to bed.\p"
            "I'm afraid you'll have to come back during the day if you'd like to adopt."
        ))
    }
    closemessage
    release
}

script ISLANDGAME_BELLEHAVEN_FOSTER_HOME_Frankie {
    lock
    if (flag(FLAG_NIGHTTIME)) {
        faceplayer
        msgbox(format(
            "{PLAYER}! How're things with Cubone?"
        ))
    }
    else {
        msgbox(format(
            "BLOO!!!{PAUSE 30} GET YOUR BOUNCY BEHIND OVER HERE AND CLEAN UP THE MESS YOU MADE!!!"
        ))
    }
    closemessage
    release
}


script ISLANDGAME_BELLEHAVEN_FOSTER_HOME_Sudowoodo {
    lock
    msgbox(format(
        "{COLOR RED}(Contrary to its appearance, it's just a toy that looks like a Sudowoodo.)"
    ))
    closemessage
    release
}

script ISLANDGAME_BELLEHAVEN_FOSTER_HOME_MrHerriman {
    lock
    faceplayer
    if(flag(FLAG_DAILY_BELLEHAVEN_ADOPTION)) {
        msgbox(format(
            "Our policy is one adoption per customer per day. Please come back tomorrow if you would like to adopt another."
        ))
        goto(MrHerriman_End)
    }

    msgbox(format(
        "Good day, young trainer. I am Mr. Herriman, the head of this foster home.\p"
        "Are you looking to adopt a Pokémon? The fee is ¥10,000."
    ), MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        msgbox(format(
            "Splendid! Who shall be your choice?"
        ))   
    }
    else {
        msgbox(format(
            "Righty oh. Inform me when you have made a decision."
        ))
        goto(MrHerriman_End)
    }

    MrHerriman_AdoptionList:
        showmoneybox(17,1)
        dynmultipush("I'll choose later!", 0)
        dynmultipush("Pichu", 1)
        dynmultipush("Azurill", 2)
        dynmultipush("Togepi", 3)
        dynmultipush("Wynaut", 4)
        dynmultipush("Tyrogue", 5)
        dynmultistack(0,0,FALSE,4,FALSE,0,DYN_MULTICHOICE_CB_NONE)
        switch (var(VAR_RESULT)) {
            case 1:
                checkmoney(10000)
                if(!var(VAR_RESULT)) {
                    msgbox(format(
                        "My apologies, but you must have the necessary funds to pay the fee."
                    ))
                    goto(MrHerriman_End)
                }
                removemoney(10000)
                updatemoneybox
                playse(SE_SHOP)
                waitse
                playse(SE_VEND)
                waitse
                hidemoneybox
                givemon(SPECIES_PICHU,1,ITEM_ORAN_BERRY,,NUM_NATURES,2,,,,,,,,31,31,31,31,31,31, MOVE_EXTREME_SPEED, MOVE_VOLT_TACKLE, MOVE_SURF, MOVE_FLY)
                playfanfare(MUS_OBTAIN_ITEM)
                bufferspeciesname(STR_VAR_1, SPECIES_PICHU)
                msgbox(format("You adopted a Pichu!"))
                waitfanfare 
                msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)

                msgbox(format(
                    "I wish all the best for you and your new companion, young trainer."
                ))
                setflag(FLAG_DAILY_BELLEHAVEN_ADOPTION)
                goto(MrHerriman_End)
            case 2:
                checkmoney(10000)
                if(!var(VAR_RESULT)) {
                    msgbox(format(
                        "My apologies, but you must have the necessary funds to pay the fee."
                    ))
                    goto(MrHerriman_End)
                }
                removemoney(10000)
                updatemoneybox
                playse(SE_SHOP)
                waitse
                playse(SE_VEND)
                waitse
                hidemoneybox
                givemon(SPECIES_AZURILL,1,ITEM_LEPPA_BERRY,,NUM_NATURES,1,,,,,,,,31,31,31,31,31,31, MOVE_AQUA_JET, MOVE_BELLY_DRUM, MOVE_SUPERPOWER, MOVE_WATER_GUN)
                playfanfare(MUS_OBTAIN_ITEM)
                bufferspeciesname(STR_VAR_1, SPECIES_AZURILL)
                msgbox(format("You adopted an Azurill!"))
                waitfanfare 
                msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)

                msgbox(format(
                    "I wish all the best for you and your new companion, young trainer."
                ))
                setflag(FLAG_DAILY_BELLEHAVEN_ADOPTION)
                goto(MrHerriman_End)
            case 3:
                checkmoney(10000)
                if(!var(VAR_RESULT)) {
                    msgbox(format(
                        "My apologies, but you must have the necessary funds to pay the fee."
                    ))
                    goto(MrHerriman_End)
                }
                removemoney(10000)
                updatemoneybox
                playse(SE_SHOP)
                waitse
                playse(SE_VEND)
                waitse
                hidemoneybox
                givemon(SPECIES_TOGEPI,1,ITEM_RAGE_CANDY_BAR,,NUM_NATURES,1,,,,,,,,31,31,31,31,31,31, MOVE_NASTY_PLOT, MOVE_GROWL, MOVE_CHARM, MOVE_NONE)
                playfanfare(MUS_OBTAIN_ITEM)
                bufferspeciesname(STR_VAR_1, SPECIES_TOGEPI)
                msgbox(format("You adopted a Togepi!"))
                waitfanfare 
                msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)

                msgbox(format(
                    "I wish all the best for you and your new companion, young trainer."
                ))
                setflag(FLAG_DAILY_BELLEHAVEN_ADOPTION)
                goto(MrHerriman_End)
            case 4:
                checkmoney(10000)
                if(!var(VAR_RESULT)) {
                    msgbox(format(
                        "My apologies, but you must have the necessary funds to pay the fee."
                    ))
                    goto(MrHerriman_End)
                }
                removemoney(10000)
                updatemoneybox
                playse(SE_SHOP)
                waitse
                playse(SE_VEND)
                waitse
                hidemoneybox
                givemon(SPECIES_WYNAUT,1,ITEM_LIECHI_BERRY,,NUM_NATURES,0,,,,,,,,,,,,,,)
                playfanfare(MUS_OBTAIN_ITEM)
                bufferspeciesname(STR_VAR_1, SPECIES_WYNAUT)
                msgbox(format("You adopted a Wynaut!"))
                waitfanfare 
                msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)

                msgbox(format(
                    "I wish all the best for you and your new companion, young trainer."
                ))
                setflag(FLAG_DAILY_BELLEHAVEN_ADOPTION)
                goto(MrHerriman_End)
            case 5:
                checkmoney(10000)
                if(!var(VAR_RESULT)) {
                    msgbox(format(
                        "My apologies, but you must have the necessary funds to pay the fee."
                    ))
                    goto(MrHerriman_End)
                }
                removemoney(10000)
                updatemoneybox
                playse(SE_SHOP)
                waitse
                playse(SE_VEND)
                waitse
                hidemoneybox
                givemon(SPECIES_TYROGUE,1,ITEM_FOCUS_BAND,,NUM_NATURES,1,,,,,,,,31,31,31,31,31,31, MOVE_HIGH_JUMP_KICK, MOVE_BULLET_PUNCH, MOVE_MACH_PUNCH, MOVE_RAPID_SPIN)
                playfanfare(MUS_OBTAIN_ITEM)
                bufferspeciesname(STR_VAR_1, SPECIES_TYROGUE)
                msgbox(format("You adopted a Tyrogue!"))
                waitfanfare 
                msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
                msgbox(format(
                    "I wish all the best for you and your new companion, young trainer."
                ))
                setflag(FLAG_DAILY_BELLEHAVEN_ADOPTION)
                goto(MrHerriman_End)
            default: // case 0
                msgbox(format(
                    "Righty oh. Inform me when you have made a decision."
                ))
                goto(MrHerriman_End)
        }

    MrHerriman_End:
        hidemoneybox
        closemessage
        release
}